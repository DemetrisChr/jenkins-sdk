buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }

}

plugins {
    id 'groovy'
    id 'java-library'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.couchbase.perf.shared.main.Execute'
        )
    }
}

sourceSets {
    main {
        groovy {
            srcDirs 'src'
            srcDirs 'vars'
        }
    }
    test {
        groovy {
            srcDirs 'test/src'
        }
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.apache.groovy:groovy-all:4.0.4'
    implementation 'org.apache.groovy:groovy-yaml:4.0.4'
    implementation 'org.yaml:snakeyaml:1.30'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3'
    implementation 'org.postgresql:postgresql:42.3.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}


task tags(type: JavaExec) {
    group = "Execution"
    description = "Run the TagsProcessor tool"
    classpath = sourceSets.main.runtimeClasspath
    main = "com.couchbase.tools.tags.TagProcessor"
}

task buildPerformer(type: JavaExec) {
    group = "Execution"
    description = "Build the Docker image for a performer"
    classpath = sourceSets.main.runtimeClasspath
    main = "com.couchbase.tools.performer.BuildPerformer"
}

task setupPerfDatabase(type: JavaExec) {
    group = "Execution"
    description = "Setup the performance database"
    classpath = sourceSets.main.runtimeClasspath
    main = "com.couchbase.tools.perf.database.SetupPerfDatabase"
}
